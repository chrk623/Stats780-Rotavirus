/*==================================================================================*/
/* The macro function adminDatabaseMerge() calls NHI_Module(), NIR_Module(), and 	*/
/* NMDS_Module() then merges the SAS databases together into called NHI_Merge.		*/
/*																					*/
/* The macro function adminDatabaseMerge() has the following arguments:			 	*/
/* 	- filepath_to_databases: The filepath of the folder containing the NHI, NIR,	*/
/* 		and NMDS SAS databases (enclosed in quote marks).							*/
/* 	- filepath_to_modules: The filepath of the folder containing the NHI_Module(),	*/
/* 		NIR_Module(), and NMDS_Module() SAS macros.									*/
/* 	- primaryKey: The name of the column which contains a person's unique			*/
/*		identifier.	Default Value = MASTER_HCU_ID.									*/
/*																					*/
/*	Example of usage:																
	%adminDatabaseMerge(filepath_to_databases = "H:\SAS_Datasets",
					   filepath_to_modules = "H:\Modules");
	* View the SAS macro variables generated by NIR_Module() and NMDS_Module();
	%PUT _User_; 

/*==================================================================================*/

%MACRO adminDatabaseMerge(filepath_to_databases, filepath_to_modules, primaryKey = MASTER_HCU_ID);
	/*
		Load each administrative database module SAS programs.
	*/
	%let filepath_to_modules = %SYSFUNC(COMPRESS(%QUOTE(&filepath_to_modules), ""));;
	%INCLUDE "&filepath_to_modules\NIR_Module.sas";
	%INCLUDE "&filepath_to_modules\NHI_Module.sas";
	%INCLUDE "&filepath_to_modules\NMDS_Module.sas";

	/*
		Create a SAS library reference which points to the provided &filepath_to_databases.
	*/
	LIBNAME Bp9P2J &filepath_to_databases;

	/*
		Run each administrative database modules with their default values.
	*/
	%NHI_Module(NHI_File = Bp9P2J.Mis3090_cohort, 
				primaryKey = &primaryKey);
	%NIR_Module(NHI_File = work.NHI_Base, 
				NIR_File = Bp9P2J.Nir_rotavirus,
				primaryKey = &primaryKey);
	%NMDS_Module(NHI_File = work.NHI_Base, 
				 NMDS_events_file = Bp9P2J.Pus9608_events, 
				 NMDS_diags_file = Bp9P2J.Pus9608_diags, 
				 primaryKey = &primaryKey, 
				 diagsTypeFlag = "A");

	/*
		Merge the three databases together.
	*/
	PROC SQL NOPRINT;
		CREATE TABLE NHI_Merge AS
		SELECT NHI_Base.*, RVirus_Transpose_A.*, NMDS_Transpose.*
		FROM NHI_base
		LEFT JOIN RVirus_Transpose_A
		ON NHI_Base.&primaryKey = RVirus_Transpose_A.&primaryKey
		LEFT JOIN NMDS_Transpose
		ON NHI_Base.&primaryKey = NMDS_Transpose.&primaryKey;
	QUIT;

	/* 
		Remove the SAS library reference created at the beginning of the macro.
	*/
	LIBName Bp9P2J;
%MEND adminDatabaseMerge;
